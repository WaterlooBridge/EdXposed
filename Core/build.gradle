import org.gradle.internal.os.OperatingSystem

apply plugin: 'com.android.library'
version "v1.1.0_beta"
extensions["module_name"] = "EdXposed"
android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
    }
    externalNativeBuild {
        cmake {
            path "jni/CMakeLists.txt"
        }
    }
}
afterEvaluate {

    android.libraryVariants.all { variant ->
        def nameCapped = variant.name.capitalize()
        def nameLowered = variant.name.toLowerCase()

        def zipTask = task("zip${nameCapped}", type: Exec, dependsOn: ":Bridge:makeAndCopy${nameCapped}") {
            workingDir '..'
            commandLine '/bin/bash', 'build.sh',\
                    project.name,\
                    "${project.version}-${nameLowered}",\
                    "${project.extensions['module_name']}"
        }

        def pushTask = task("push${nameCapped}", type: Exec) {
            workingDir 'release'
            def commands = ["adb", "push", "magisk-${project.extensions['module_name']}" +
                    "-${project.version}-${nameLowered}.zip", "/sdcard/"]
            if (OperatingSystem.current().isWindows()) {
                commandLine 'cmd', '/c', commands.join(" ")
            } else {
                commandLine commands
            }
        }

        pushTask.dependsOn(zipTask)
    }

}